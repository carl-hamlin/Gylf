;================================================================================================================================================================================================
;
;   ./functions/socket/socket.send/t
;
;   This function sends data out on a connected socket.
;
;   Assumptions:    Stack Frame:  dword - Index to data packet.
;                   esi - Index to active socket.
;
;                   - OR -
;
;                   Stack Frame:  dword - Length of buffered data.
;                                 dword - Pointer to buffered data
;                   esi - Index to active socket.
;
;   Returns:        eax - Error indicator.
;
;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;
;   Symbolic Cross-Referencing
;
;   connection.descriptor.index         ./functions/system.check/b
;
;   socket.send.buf.splice              ./functions/socket.send/t
;
;   sys.socket.call                     ./functions/socket/b
;
;   sys.socket.send                     ./functions/socket/socket.send/b
;
;   sys.system.call                     ./functions/b
;

    socket.send:            pop   ebp                                     ; Preserve return address.

                            call  get.text.data                           ; Retrieve indexed data.

    socket.send.buf.splice: push  edx                                     ; Tell send function how long the data string is.
                            push  ecx                                     ; Point send function to the data to be sent.
                            push  dword [esi+connection.descriptor.index] ; Point send function to active descriptor

                            mov   eax, sys.socket.call                    ; eax - Socket call indicator.
                            mov   ebx, sys.socket.send                    ; ebx - Send data out on a socket.
                            mov   ecx, esp                                ; ecx - Prepared to be used as pointer to arguments package for send function.
                            int   sys.system.call                         ; Send data out on a socket.
                  
                            push  ebp                                     ; Restore return address.
                            ret   (dword.l * 3)                           ; Return to caller.

    socket.send.buf:        pop   ebp                                     ; Preserve return address.

                            pop   ecx                                     ; ecx - Pointer to buffered data.
                            pop   edx                                     ; edx - Length of buffered data.

                            jmp   socket.send.buf.splice                  ; Splice the buffered data into the arguments package.
