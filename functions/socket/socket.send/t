;================================================================================================================================================================================================
;
;   ./functions/socket/socket.send/t
;
;   This function sends data out on a connected socket.
;
;   Assumptions:    Stack Frame:  dword - Index to data packet.
;                   esi - Index to active socket.
;
;                   - OR -
;
;                   Stack Frame:  dword - Length of buffered data.
;                                 dword - Pointer to buffered data
;                   esi - Index to active socket.
;
;   Returns:        eax - Error indicator.
;
;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;
;   Symbolic Cross-Referencing
;
;   connection.descriptor.index ./functions/system.check/b
;
;   dword.l                     ./b
;
;   memory.switch               ./functions/memory.switch/t
;
;   socket.send.buf.splice      ./functions/socket.send/t
;
;   socket.send.d               ./functions/socket.send/b
;
;   socket.send.l               ./functions/socket.send/b
;
;   socket.send.p               ./functions/socket.send/b
;
;   sys.socket.call             ./functions/socket/b
;
;   sys.socket.send             ./functions/socket/socket.send/b
;
;   sys.system.call             ./functions/b
;

    socket.send:            pop   ebp                                     ; Preserve return address.
                            pop   eax                                     ; Preserve index to data to be sent.

                            push  ebp                                     ; Preserve return address.

                            push  eax                                     ; Point get.text.data to data index.
                            call  get.text.data                           ; Retrieve indexed data.

                            pop   ebp                                     ; Restore return address.

    socket.send.buf.splice: push  0                                       ; Preserve flags.
                            push  edx                                     ; Preserve length of the data string.
                            push  ecx                                     ; Preserve pointer to the data to be sent.
                            push  dword [esi+connection.descriptor.index] ; Preserve active descriptor.

                            pop   dword [socket.send.d]                   ; Point send function to active descriptor.
                            pop   dword [socket.send.p]                   ; Point send function to the data to be sent.
                            pop   dword [socket.send.l]                   ; Tell send function how long the data is.
                            pop   dword [socket.send.f]                   ; Set flags for send functions.

                            mov   eax, sys.socket.call                    ; eax - Socket call indicator.
                            mov   ebx, sys.socket.send                    ; ebx - Send data out on a socket.
                            mov   ecx, socket.send.d                      ; ecx - Pointer to arguments package for send function.
                            int   sys.system.call                         ; Send data out on a socket.

                            push  0                                       ; Ask memory.switch to free memory.
                            call  memory.switch                           ; Free memory.

                            push  ebp                                     ; Restore return address.
                            ret                                           ; Return to caller.

    socket.send.buf:        pop   ebp                                     ; Preserve return address.

                            pop   ecx                                     ; ecx - Pointer to buffered data.
                            pop   edx                                     ; edx - Length of buffered data.

                            jmp   socket.send.buf.splice                  ; Splice the buffered data into the arguments package.
