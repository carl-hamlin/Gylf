;================================================================================================================================================================================================
;
;   ./functions/socket/recv.from.socket/t
;
;   This function receives data from a connected socket and places it in a specified buffer.
;
;   Assumptions:    esi = Index to connection tables.
;
;   Returns:        None.
;
;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;
;   Symbolic Cross-Referencing
;
;   buffer.1                            ./functions/b
;
;   buffer.1.l                          ./functions/b
;
;   buffer.strip                        ./functions/buffer.strip/t
;
;   connection.descriptor.index         ./functions/main/system.check/b
;
;   memory.switch                       ./functions/memory.switch/t
;
;   sys.socket.call                     ./functions/socket/b
;
;   sys.socket.recv                     ./functions/socket/recv.from.socket/b
;
;   sys.system.call                     ./functions/b
;

    recv.from.socket: push  buffer.1.l                              ; Tell receive function how long the buffer is.
                      push  buffer.1                                ; Point receive function to buffer.1.
                      push  dword [esi+connection.descriptor.index] ; Point receive function to active socket.

                      push  12                                      ; Request 12 new bytes of memory.
                      call  memory.switch                           ; Extend memory.

                      pop   dword [ebx]                             ; Point receive function to descriptor for open socket.
                      pop   dword [ebx+4]                           ; Tell receive function where to put incoming data.
                      pop   dword [ebx+8]                           ; Tell receive function how long the buffer is.

                      mov   eax, sys.socket.call                    ; eax - Socket call indicator.
                      mov   ecx, ebx                                ; ecx - Pointer to arguments package.
                      mov   ebx, sys.socket.recv                    ; ebx - Receive from socket.
                      int   sys.system.call                         ; Receive from socket.

                      push  eax                                     ; Preserve number of characters received.

                      push  0                                       ; Tell memory switch to free the temporary memory.
                      call  memory.switch                           ; Free up the temporary memory.

                      pop   eax                                     ; Restore number of characters received.

                      mov   ebx, buffer.1                           ; ebx - pointer to received data
                      call  buffer.strip                            ; Strip carriage returns from received data.

                      ret                                           ; Return to caller.
