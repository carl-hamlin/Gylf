;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;
;   ./functions/set.single.attribute/t
;
;   This function sends a vt100 escape sequence to the remote terminal which changes a single attribute about the
;   printed text. Attributes can be found in the file "attribute.control.dat".
;
;   Assumptions:    eax - Pointer to attribute to insert.
;                   esi - Pointer to descriptor associated with target connection.
;
;   Returns:        None.
;
;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;
;   Symbolic Cross-Referencing
;
;   a.p.data.gylf                     ./b
;
;   buffer.1                          ./functions/b
;
;   dword.l                           ./b
;
;   gylf.connection.descriptor.index  ./s
;
;   set.att.mod.1                     ./functions/set.single.attribute/d
;
;   set.att.mod.3                     ./functions/set.single.attribute/d
;
;   socket.send.buf                   ./functions/socket/socket.send/t
;

    set.single.attribute: push  esi                                                       ; Preserve pointer to target connection.

                          mov   esi, set.att.mod.1                                        ; esi - Pointer to first portion of the set attribute escape sequence.
                          mov   edi, buffer.1                                             ; edi - Pointer to data buffer.
                          mov   ecx, 002h                                                 ; ecx - Size of first portion of the set attribute escape sequence.

                          repnz movsb                                                     ; Copy the first portion of the set attribute escape sequence to the data buffer.

                          mov   esi, eax                                                  ; esi - Pointer to attribute to be set.

                          movsb                                                           ; Copy the attribute to be set to the data buffer.

                          mov   esi, set.att.mod.3                                        ; esi - Pointer to the last portion of the set attribute escape sequence.

                          movsb                                                           ; Copy the last portion of the set attribute escape sequence to the data buffer.

                          pop   esi                                                       ; Restore pointer to target connection.

                          push  dword [a.p.data.gylf + gylf.connection.descriptor.index]  ; Point socket.send to the active socket.
                          push  buffer.1                                                  ; Point socket.send to the data buffer.
                          push  dword.l                                                   ; Provide the length of the buffer.
                          push  00000000h                                                 ; Flags for send function.
                          call  socket.send                                               ; Send the string to the socket and change the attribute remotely.

                          ret                                                             ; Return to caller.
