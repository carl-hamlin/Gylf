;================================================================================================================================================================================================
;
;   ./functions/aggregate.arguments/t
;
;   This file contains code pertinent to the separation and filing of commands and arguments received from an incoming socket.
;
;   Assumptions: [esp+4] - Pointer to data received from the socket.
;
;   Returns:      ebx               - Pointer to original arguments.
;                [argument.packet]  - Aggregated arguments.
;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;
;   Symbolic Cross-Referencing
;
;   aggregate.arguments.argument.found          ./functions/aggregate.arguments/t
;
;   aggregate.arguments.arguments.aggregated    ./functions/aggregate.arguments/t
;
;   aggregate.arguments.loop                    ./functions/aggregate.arguments/t
;
;   aggregate.arguments.prep.for.next.argument  ./functions/aggregate.arguments/t
;
;   aggregate.arguments.quote.mode              ./functions/aggregate.arguments/t
;
;   aggregate.arguments.quode.mode.l            ./functions/aggregate.arguments/t
;
;   aggregate.arguments.strip.command.loop      ./functions/aggregate.arguments/t
;
;   argument.01.location                        ./functions/aggregate.arguments/b
;
;   argument.entry.size                         ./functions/aggregate.arguments/b
;
;   arguments.num                               ./functions/aggregate.arguments/b
;

    aggregate.arguments:                        pop   edx                                         ; Preserve return address.
                                                pop   ebx                                         ; ebx - Pointer to arguments to be aggregated.
                                                push  edx                                         ; Restore return address.

                                                push  eax                                         ; Preserve caller's eax register.
                                                push  ebx                                         ; Preserve pointer to arguments.
                                                push  esi                                         ; Preserve pointer to data received from the socket.
                                                
                                                sub   eax, eax                                    ; eax - Prepared to be used for counting.
                                                sub   edx, edx                                    ; edx - Prepared to be used to count arguments.

    aggregate.arguments.strip.command.loop:     cmp   byte [ebx], 00h                             ; Are we looking at a zero terminator?
                                                jz    aggregate.arguments.arguments.aggregated    ; Yes - this indicates that there are no arguments. Finish aggregating.
     
                                                cmp   byte [ebx], 20h                             ; Are we looking at a space?
                                                jz    aggregate.arguments.argument.found          ; Yes. This indicates that there is at least one argument. Proceed to the aggregator.
                                                
                                                inc   ebx                                         ; ebx - Pointer to next byte in command.
                                                jmp   aggregate.arguments.strip.command.loop      ; Check the next byte.
                      
    aggregate.arguments.argument.found:         inc   ebx                                         ; ebx - Pointer to first argument.
                                                
                                                mov   esi, argument.01.location                   ; esi - Pointer to first location.
                                                mov   dword [esi], ebx                            ; Populate location of first argument.

    aggregate.arguments.loop:                   cmp   byte [ebx], '"'                             ; Are we looking at a quotation mark?
                                                jz    aggregate.arguments.quote.mode              ; Yes - this is a special case argument. Go treat it as such.

                                                cmp   byte [ebx], 20h                             ; Are we looking at a space?
                                                jz    aggregate.arguments.prep.for.next.argument  ; Yes - under normal circumstances, this indicates a new argument.

                                                cmp   byte [ebx], 00h                             ; Are we looking at a zero terminator?
                                                jz    aggregate.arguments.prep.for.next.argument  ; Yes - this indicates that we're finished.

                                                inc   ebx                                         ; ebx - Pointer to next byte in argument.
                                                inc   eax                                         ; eax - Incremented to reflect an additional byte in the size of the argument.

                                                jmp   aggregate.arguments.loop                    ; Go check the next byte.

    aggregate.arguments.prep.for.next.argument: inc   ebx                                         ; ebx - Pointer to next argument or terminator.
                                                inc   edx                                         ; edx - Incremented to indicate an increase in the number of arguments.
                                                mov   dword [esi-4], eax                          ; Store size of argument recently examined.
                                                sub   eax, eax                                    ; eax - Prepared to be used for counting.

                                                cmp   byte [ebx], 00h                             ; Are we looking at a zero terminator?
                                                jz    aggregate.arguments.arguments.aggregated    ; Yes - We're done here.

                                                add   esi, argument.entry.size                    ; esi - Pointer to next argument entry.
                                                mov   dword [esi], ebx                            ; Store location of next argument.

                                                jmp   aggregate.arguments.loop                    ; Go check the next argument.

    aggregate.arguments.arguments.aggregated:   mov   dword [arguments.num], edx                  ; Store the number of arguments.

                                                pop   esi                                         ; Restore pointer to data received from the socket.
                                                pop   ebx                                         ; Restore pointer to arguments.
                                                pop   eax                                         ; Restore caller's eax register.

                                                ret                                               ; Return to caller.

    aggregate.arguments.quote.mode:             dec   eax                                         ; eax - Decremented to offset first loop iteration.

                                                inc   ebx                                         ; ebx - Pointer to the beginning of the argument.

                                                mov   dword [esi], ebx                            ; Populate location of argument.

    aggregate.arguments.quote.mode.l:           inc   ebx                                         ; ebx - Pointer to next byte in quoted argument.
                                                inc   eax                                         ; eax - Incremented to reflect an additional byte in the size of the argument.

                                                cmp   byte [ebx], '"'                             ; Have we found the terminating quotation mark?
                                                jnz   aggregate.arguments.quote.mode.l            ; No. Go check the next byte.

                                                inc   ebx                                         ; ebx - Pointer to delineator between this argument and the next, or terminator.
                                                inc   eax                                         ; eax - Size of quoted argument.

                                                jmp   aggregate.arguments.prep.for.next.argument  ; Go see if we have another argument.
