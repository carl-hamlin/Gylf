;================================================================================================================================================================================================
;
;   ./commands/help/t
;
;   This function displays context-sensitive help.
;
;   Assumptions:  ebp - Pointer to populated answer.active.socket.packet.
;                 Arguments aggregated.
;
;   Returns:      None.
;
;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;
;   Symbolic Cross-Referencing
;
;   ./b                                         dword.l
;
;   ./commands/help/t                           command.help.found.command
;                                               command.help.next.command
;                                               command.help.next.command.jcxnz
;                                               command.help.no.arguments
;                                               command.help.parse.arguments
;
;   ./functions/aggregate.arguments/d           argument.01.location
;                                               arguments.num
;
;   ./functions/d                               command.table
;                                               number.of.commands
;
;   ./functions/socket/socket.send/t            socket.send
;
;   ./functions/write.bad.command.error/t       write.bad.command.error
;
;   ./functions/write.bad.help.command.error/t  write.bad.help.command.error
;

    command.help:                     push  eax                                             ; Preserve length of received data.
                  
                                      cmp   byte [arguments.num], 00h                       ; Do we have arguments?
                                      jz    command.help.no.arguments                       ; No. Run without the argument parser.
                  
                                      pop   eax                                             ; Restore length of received data.
    
                                      push  esi                                             ; Preserve caller's esi.
                                      push  edi                                             ; Preserve caller's edi.

                                      mov   ebx, command.table                              ; ebx - Index to command table.
                                      mov   esi, dword [ebx]                                ; esi - Pointer to command table entry.
                                      mov   edi, [argument.01.location]                     ; edi - Pointer to argument.
                                      
                                      mov   ecx, number.of.commands                         ; ecx - Loop counter for iteration through possible commands.
                  
    command.help.parse.arguments:     push  ecx                                             ; Preserve loop counter.
                                      sub   ecx, ecx                                        ; ecx - Prepared to be used as string length indicator.
                                      mov   cl, byte [esi]                                  ; cl - length of command in table.
                                      inc   esi                                             ; esi - Pointer to string data corresponding to command currently being referenced in the table.

                                      cmp   cl, al                                          ; Is the length of the received data at least the same as the command in the table?
                                      ja    command.help.next.command                       ; No. It's not necessary to perform any further comparison; the received data cannot possibly refer
                                                                                            ; to the command currently being read from the table. Move on to the next table entry.

                                      repz  cmpsb                                           ; Check to see if the received data is the same as the string data corresponding to the command
                                                                                            ; currently being referenced in the table.
                                      jnz   command.help.next.command.jcxnz                 ; Otherwise, go set up for the next iteration.
                                      jcxz  command.help.found.command                      ; Did we get all the way through the string before cmpsb cut us loose? If so, go set up the handler
                                                                                            ; and jump to it.

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

    command.help.found.command:       pop   ecx                                             ; Restore loop counter.
                                      pop   edi                                             ; Restore caller's edi register...

                                      mov   ebx, esi                                        ; ebx - Pointer to command entry.
                                      mov   ebx, dword [ebx+dword.l]                        ; ebx - Pointer to index for help entry.

                                      pop   esi                                             ; esi - Index to socket entry.

                                      push  dword [ebx]                                     ; Point send function to help message index.
                                      call  socket.send                                     ; Send the message.
                                      
                                      ret                                                   ; Return to caller.

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

    command.help.next.command.jcxnz:  mov   edi, [argument.01.location]                     ; edi - Pointer to argument.

;------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

    command.help.next.command:        add   ebx, dword.l                                    ; ebx - Pointer to next entry pointer.
                                      mov   esi, dword [ebx]                                ; esi - Pointer to next entry.

                                      pop   ecx                                             ; Restore loop counter.
                                      loop  command.help.parse.arguments                    ; Go check the next command.

                                      pop   edi                                             ; Restore caller's edi register.
                                      pop   esi                                             ; esi - Index to socket entry.

                                      call  write.bad.help.command.error                    ; Let the user know that they've entered a bad command and suggest that they try HELP.

                                      ret                                                   ; Return to caller.

    command.help.no.arguments:        pop   eax                                             ; Restore length of received data.
    
                                      call  write.bad.command.error                         ; Placeholder function to let me know that no arguments is getting executed.

                                      ret                                                   ; Return to caller.
